#pragma library
proc the_formsave (f,b) {
  f.same
| f.key; f.save; { b.browse_seek(conc(f.id_key,'=',f.key)); f.load | f.init }
| f.insert; f.load;
  b.raise('onrowclick',b.browse_seek(conc(f.id_key,'=',f.key)))
}
# -------------------------------------------------------------------------------------- Dokumentace
panel apl [,,770,] {type:'right', title:'Dokumentace', _sys:'dok'
  menu m {type:'left'                   // nesmí mít active
    proc onclick(x) {
      u.fill(ask('i_doc_show','application',x.owner._id,x._id))
#      echo(u,apl.u)
    }
  }
  use u: form f [0,0,,]
  proc onfirstfocus() { set_apl_menu }  // nesmí být v onstart
  proc set_apl_menu() {
    var g: object
    m.attach_code(ask('i_doc_menu','application','',''));
    g.set(m.part('menu.group',1));
    g.get; g.part('item',1); g.part('item',1).click
  | u.fill("Dokumentace není vygenerována")
  }
}
# ---------------------------------------------------------------------------------------- Reference
panel ref [,,770,] {type:'right', title:'Reference', _sys:'ref', skill:'m'
  menu m {type:'left', active:*
    proc onclick(x) {
      u.fill(ask('i_doc_show','reference',x.owner._id,x._id))
    }
  }
  use u: form f [0,0,,]
  proc onstart() { m.attach_code(ask('i_doc_menu','reference','','')) }
}
# ---------------------------------------------------------------------------------------- Struktura
panel str [,,770,] {type:'right', title:'Struktura', _sys:'str', skill:'m'
  menu m {type:'left', active:*
    menu stav {title:'stav',type:'group'
      item {title:'Ezer moduly' proc onclick () { u.fill(ask('doc_ezer')) } }
      item {title:'PHP moduly' proc onclick () { u.fill(ask('doc_php')) } }
      item {title:'Seznam PHP funkcí' proc onclick () { u.fill(ask('doc_called')) } }
    }
  }
  use u: form f [0,0,,]
}
# --------------------------------------------------------------------------------------- Generování
panel gen [,,770,] {type:'right', title:'Generování', _sys:'gen', skill:'m'
  menu m {type:'left', active:*
    menu stav {title:'stav',type:'group'
      item {title:'přehled' proc onclick () {
        u.fill("<h1>Přehled systémové dokumentace</h1>");
        u.append(ask('i_doc','survay'))
      } }
#       item {title:'tabulka funkcí' proc onclick () { u.fill(ask('i_doc','funcs')) } }
#       item {title:'tabulka atributů' proc onclick () { u.fill(ask('i_doc','attrs')) } }
    }
    menu {title:'PSPad',type:'group'
      item a {title:'generuj Ezer.ini'
        proc onclick () { u.fill(ask('pspad_gen')) }
      }
    }
    menu refr {title:'obnovit',type:'group'
      item {title:'systémová dokumentace'
        proc onclick () {
          u.fill("<h1>Reset systémové dokumentace</h1>");
          u.append(ask('i_doc_reset','ezerscript'));
          u.append(ask('i_doc_reset','reference'));
          u.append(ask('i_doc','ezerscript'));
          u.append(ask('i_doc','reference',conc(sys('version'),'/wiki/.*\.wiki')));
          u.append(ask('i_doc','javascript','app,ezer,ezer_report,ezer_fdom1,ezer_fdom2'));
          ref.m.attach_code(ask('i_doc_menu','reference','',''))
      }}
      item {title:'dokumentace aplikace'
        proc onclick () {
          var txt: text
          var patt: text
          u.fill("<h1>Reset aplikační dokumentace</h1>");
          patt.set(conc('wiki/(',sys('root'),'|ezer)_.*\.wiki'));
          u.append(ask('i_doc_reset','application'));
          txt.set(ask('i_doc','application',patt));
          txt.get;
          u.append(txt);
          apl.focus;
          apl.set_apl_menu
        | u.append(conc("Nebyly nalezeny žádné soubory nápovědy vyhovující: <b>/",patt,"/</b>"))
      }}
    }
    menu dele {title:'údržba',type:'group'
      item a {title:'smazat vše'
        proc onclick () {
          u.fill("<h1>Reset dokumentace</h1>");
          u.fill(ask('i_doc_reset'));
          ref.m.attach_code(ask('i_doc_menu','reference','',''));
          apl.m.attach_code(ask('i_doc_menu','application','',''))
      }}
    }
    proc onclick(x) { echo('menu',x._id) }
  }
  use u: form f [0,0,,]
}
# ------------------------------------------------------------------------------------------ Novinky
panel nov [,,770,] {type:'right', title:'Novinky', _sys:'new'
  menu m {type:'left', active:m.app.nove
    menu app {title:'Novinky aplikace',type:'group'
      item nove {title:'Nové' }
      item stare {title:'Přehled starších' }
      item todo {title:'Požadavky na doplnění' }
      proc onclick(i) { u.fill(ask('doc_todo',i._id,'app')) }
    }
    menu eze {title:'Novinky jádra systému Ezer2',type:'group', skill:'m'
      item nove {title:'Nové' }
      item stare {title:'Přehled starších' }
      item todo {title:'Požadavky na doplnění' }
      proc onclick(i) { u.fill(ask('doc_todo',i._id,'sys')) }
    }
  }
  use u: form f [0,0,,]
}
# ---------------------------------------------------------------------------------- kontextový help
panel faq [,,770,] {type:'right', title:'Kontextový help', _sys:'faq'
  use hlp: form _hlp [12,4,,]
  menu {type:'left', active:*
    menu {title:'Přehledy', type:'group'
      item {title:'Seznam všech' }
      proc onclick(i) {
        hlp.fill(conc(i.owner.title,' - ',i.title));
        hlp.s.browse_load
      }
    }
  }
  form _hlp [,,*,500] {
    // seznam témat
    browse s [0,35,150,100] { rows:20, qry_rows:1
      show id_help { data:_help.id_help }
      show [,,100,] { title:'název', data:_help.name, format:'s+q*t' }
      show [,,50,] { title:'zkratka', data:_help.topic, format:'sq*t' }
      show [,,40,] { title:'stav', data:_help.seen, format:'sq*t' }
      proc onrowclick() {
        form.key(id_help.get); form.load;
        hlp.set(f_hlp.get)
      }
    }
    // pole
    field f_hlp { data:_help.help }
    label head [0,0,*,31] { title:'' }
    label [230,41,0,] { title:'název:' }
    field name [267,37,119,] { data:_help.name }
    label [395,41,31,] { title:'zkratka:' }
    field topic [440,37,80,] { data:_help.topic,
      help:"zřetězení atributů _sys pro kontext nebo 'ezer.cokoliv' pro obecný text" }
    label [529,41,27,] { title:'stav:' }
    field seen [558,37,40,] { data:_help.seen }
    // příkazy
    button [609,36,,] { title:"Nový"
      proc onclick() { form.init(1) }
    }
    button [648,36,,] { title:"Ulož"
      proc onclick() { the_formsave(form,s) }
    }
    button [686,36,,] { title:"Uprav text"
      proc onclick() {
        help_edit.h.txt.set(f_hlp.get);
        help_edit.modal(50,50);
        f_hlp.set(help_edit.h.txt.get);
        f_hlp.change;
        form.save; form.load; hlp.set(f_hlp.get)
      }
    }
    // plocha pro zobrazení téma
    label hlp [230,66,,] { title:'', css:'ContextHelp'
      style:"width:auto;margin-right:20px;font-size:11px;color:0;background-color:#C9D5E5;padding:10px" }
    // procedury
    proc fill(h) {
      [ h; head.set(conc("<div class='karta'>",h,"</div>")) ];
    }
  }
  # ================================================================================= help_edit
  panel help_edit [0,0,645,520] { title:' Úprava textu nápovědy', type:'popup', css:'dialog'
    use h: form _hlp [0,0,,],
    # ------------------------------------------------------------------------ _hlp
    form _hlp [10,10,600,460] {
      button  [540,9,45,20] { title:'Uložit', help:'ukončit editor a uložit změny'
        proc onclick() {panel.hide(1); }
      }
      button  [600,9,45,20] { title:'Zpět', help:'ukončit editor bez uložení změn'
        proc onclick() { panel.hide(0); }
      }
      edit txt [0,40,655,480] {type:'html', par:°{toolbar:'EzerMail'} },
    }
  }
}
# ------------------------------------------------------------------------------------------------ f
# formulář pro levostranné menu
form f {
  proc fill(txt) {
    hresult.set(conc("<div id='Content'>",txt,"</div>"));
  }
  proc append(txt) {
    hresult.set(conc(hresult.get,"<div id='Content'>",txt,"</div>"));
  }
  label hresult [0,0,*,] { title:'--' }
}

table _help {  key_id:'id_help'
  number id_help { key:'primary' }
  text topic
  text name
  text seen
  text help
}
